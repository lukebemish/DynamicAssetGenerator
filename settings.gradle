pluginManagement {
    repositories {
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            name = 'NeoForged'
            url = 'https://maven.neoforged.net/'
        }
        maven {
            name = "Luke's Maven"
            url = 'https://maven.lukebemish.dev/releases/'
        }
        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }
        maven {
            name = 'Architectury'
            url "https://maven.architectury.dev/"
        }

        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id 'dev.lukebemish.conventions' version '0.1.9'
    id 'dev.lukebemish.multisource' version '0.1.6'
}

rootProject.name = 'dynamicassetgenerator'

multisource.of(':') {
    configureEach {
        minecraft.add project.libs.minecraft
        mappings.add loom.layered() {
            officialMojangMappings()
            parchment(project.dependencies.variantOf(project.libs.parchment) {
                artifactType('zip')
            })
        }
    }
    common('main', []) {}
    fabric('fabric', ['main']) {}
    neoforge('neoforge', ['main']) {
        neoForge.add project.libs.neoforge
    }
    repositories {
        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }
        maven {
            url = 'https://maven.wispforest.io'
            content {
                includeModule('io.wispforest', 'owo-lib')
            }
        }
    }
}
